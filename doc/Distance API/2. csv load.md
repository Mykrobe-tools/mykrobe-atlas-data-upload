Once you have all your CSV files from the step 1 ready, you can start loading the data in
those files into a Neo4J database. Before you do that, make sure you have the Neo4J instance
serving the Distance API is running.

###Upload CSVs

In Neo4J, there is default directory where you can store the CSVs for import. By default, it
is `/var/lib/neo4j/import/`. But this could change due to configuration. Neo4J will only allow
you to import CSVs from this directory only due to security restriction.

The very first step in CSV load process is to upload the CSV file generate from last process
to above mentioned directory.
```
kubectl cp neo4j.tree.ids.csv mykrobe-dev/neo4j-deployment-7df6ff7944-tz6ll:/var/lib/neo4j/import/
kubectl cp neo4j.dev.sample.ids.csv mykrobe-dev/neo4j-deployment-7df6ff7944-tz6ll:/var/lib/neo4j/import/
kubectl cp neo4j.dev.sample.nearest.leaf.csv mykrobe-dev/neo4j-deployment-7df6ff7944-tz6ll:/var/lib/neo4j/import/
kubectl cp neo4j.dev.sample.nearest.neighbours.csv mykrobe-dev/neo4j-deployment-7df6ff7944-tz6ll:/var/lib/neo4j/import/
```

###Login

Open a shell from Neo4J instance:
```
kubectl exec -ti <neo4j-pod-name> -n <namespace> bash
bin/cypher-shell -u neo4j
# Now enter the password
```

###Create Indexes
```
CREATE CONSTRAINT experiment_id_unique_constraint ON (sample:SampleNode) ASSERT sample.experiment_id IS UNIQUE;
CREATE CONSTRAINT leaf_id_unique_constraint ON (leaf:LeafNode) ASSERT leaf.leaf_id IS UNIQUE;
``` 

**Sanity check**
```
CALL db.indexes;
CALL db.constraints;
```

###Create `LeafNode`
```
USING PERIODIC COMMIT 1000
LOAD CSV FROM 'file:///neo4j.dev.tree.ids.csv' AS line
CREATE (:LeafNode { leaf_id: line[0] });
```

###Create `SampleNode`
```
USING PERIODIC COMMIT 1000
LOAD CSV FROM 'file:///neo4j.dev.sample.ids.csv' AS line
CREATE (:SampleNode { experiment_id: line[0] });
```

###Insert Nearest Leaf
```
USING PERIODIC COMMIT 1000
LOAD CSV FROM 'file:///neo4j.dev.sample.nearest.leaf.csv' AS line
MATCH(n:SampleNode {experiment_id: line[0]}), (leaf:LeafNode {leaf_id: line[1]})
CREATE (n)-[:LINEAGE{distance: toInteger(line[2])}]->(leaf);
```

###Insert Nearest Neighbours
```
USING PERIODIC COMMIT 1000
LOAD CSV FROM 'file:///neo4j.dev.sample.nearest.neighbours.csv' AS line
MATCH(n:SampleNode {experiment_id: line[0]}), (neighbour:SampleNode {experiment_id: line[1]})
CREATE (n)-[:NEIGHBOUR{distance: toInteger(line[2])}]->(neighbour), (neighbour)-[:NEIGHBOUR{distance: toInteger(line[2])}]->(n);
```
